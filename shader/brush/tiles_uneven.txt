// Copyright (c) 2021 Lachlan McDonald
// This work is licensed under the MIT License (MIT)
// https://github.com/lachlanmcdonald/magicavoxel-shaders
//
// xs brush/tiles_uneven [Mode] [Min] [Max] [Line Color] [Line Width] [Noise] [Seed]
//
// xs_begin
// author : '@lachlanmcdonald'
// arg : { id = '0'  name = 'Mode'  value = '0'  var = 'm_mode'  range = '0 5'  step = '1'  decimal = '0' }
// arg : { id = '1'  name = 'Min'  value = '4'  var = 'm_min'  range = '1 256'  step = '1'  decimal = '0' }
// arg : { id = '2'  name = 'Max'  value = '8'  var = 'm_max'  range = '1 256'  step = '1'  decimal = '0' }
// arg : { id = '3'  name = 'Line Color'  value = '16'  var = 'm_line_color'  range = '0 255'  step = '1'  decimal = '0' }
// arg : { id = '4'  name = 'Line Width'  value = '1'  var = 'm_line_width'  range = '0 256'  step = '1'  decimal = '0' }
// arg : { id = '5'  name = 'Noise'  value = '0'  var = 'm_noise'  range = '0 100'  step = '1'  decimal = '1' }
// arg : { id = '6'  name = 'Seed'  value = '1'  var = 'm_seed'  range = '1 100'  step = '1'  decimal = '0' }
// xs_end

int mode = int(m_mode);
float thickness = m_line_width;
float size_min = min(m_min, m_max) + thickness;
float size_max = max(m_min, m_max) + thickness;
float line_color = m_line_color;
float noise = clamp(m_noise / 100.0, 0.0, 1.0);
float global_seed = mix(-437.5453, 437.5453, m_seed / 100.0);

struct Series {
	float index;
	float size;
	float local;
};

float random(vec2 co, float seed) {
	return fract(cos(dot(co.xy, vec2(23.14069266477926, pow(sqrt(2.0), 2.0)))) * (43758.6980280632 + global_seed + seed));
}

float random(vec2 co) {
	return random(co, 0.0);
}

Series find_series(int limit, vec2 seed_multiplier) {
	float cell_index = 0.0;
	float cell_size = 0.0;
	int last_index = 0;

	for (int i = 0; i <= limit;) {
		vec2 seed = vec2(float(i) * size_max) * seed_multiplier;

		last_index = i;
		cell_index += 1.0;
		cell_size = floor(mix(size_min, size_max + 1.0, random(seed)));
		i += int(cell_size);
	}

	float local_pos = (cell_index == 0.0) ? float(limit) : float(limit - last_index);
	Series j = Series(cell_index, cell_size, local_pos);
	return j;
}

float pal(float p) {
	float f = floor(mix(0.0, float(i_num_color_sels), p));
	return color_sel(f);
}

float pal_noise(float p, vec3 v) {
	if (noise > 0.0) {
		float d = random(v.xy, global_seed * 2.0);
		return d <= noise ? pal(d) : pal(p);
	} else {
		return pal(p);
	}
}

float map(vec3 v) {
	Series x = find_series(int(v.x), vec2(64.0,  0.0));

	float offset = ceil(mix(size_min, size_max, random(vec2(x.index, 0.0))));
	float k = mod(x.index, 2.0) == 0.0 ? v.y : v.y + offset;
	Series y = find_series(int(k), vec2( 0.0, 64.0));

	vec2 index = vec2(x.index, y.index);
	vec2 local = vec2(x.local, y.local);
	vec2 size = vec2(x.size, y.size);

	if (all(lessThan(local.xy, size.xy - vec2(thickness)))) {
		if (mode == 0) {
			float f = random(index.xy);
			return pal_noise(f, v);
		} else if (mode == 1) {
			float f = smoothstep(size_min * size_min,
								 size_max * size_max,
								 size.x * size.y);
			return pal_noise(f, v);
		} else if (mode == 2) {
			float f = mod(index.x, float(i_num_color_sels)) / float(i_num_color_sels);
			return pal_noise(f, v);
		} else if (mode == 3) {
			float f = mod(index.y, float(i_num_color_sels)) / float(i_num_color_sels);
			return pal_noise(f, v);
		} else if (mode == 4) {
			float f = random(index.xy);
			return f > 0.5 ? pal_noise(0.0, v) : pal_noise(1.0, v);
		} else if (mode == 5) {
			return mod(index.y, 2.0) == 0.0 ? pal_noise(0.0, v) : pal_noise(1.0, v);
		}
	} else {
		return line_color;
	}
}
