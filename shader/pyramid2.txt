// MIT License (MIT)
// https://github.com/lachlanmcdonald/magicavoxel-shaders
// Copyright (c) 2020 Lachlan McDonald
//
// xs pyramid2 [Color]
//
// xs_begin
// author : '@lachlanmcdonald'
// arg : { id = '0'  name = 'Color'  value = '0'  range = '0 255'  step = '1'  decimal = '0' }
// xs_end

vec2 vol = vec2(i_volume_size.xy - 1.0);

bool no_axis_mode = all(equal(ivec3(i_axis), ivec3(0)));
bvec3 axis_mode = no_axis_mode ? bvec3(true) : equal(ivec3(i_axis), ivec3(1));

float map(vec3 v) {
	float index = voxel(v);

	if (index == 0.0) {
		if (voxel(vec3(v.x, v.y, v.z - 1.0)) == i_color_index) {
			float t = 0.0;
			float a = 0.0;
			float b = 0.0;

			if (axis_mode.x) {
				t += 2.0;
				a = float((v.x == 0.0   ? 0.0 : voxel(vec3(v.x - 1.0, v.y, v.z - 1.0))) > 0.0) +
					float((v.x == vol.x ? 0.0 : voxel(vec3(v.x + 1.0, v.y, v.z - 1.0))) > 0.0);
			}

			if (axis_mode.y) {
				t += 2.0;
				b = float((v.y == 0.0   ? 0.0 : voxel(vec3(v.x, v.y - 1.0, v.z - 1.0))) > 0.0) +
					float((v.y == vol.y ? 0.0 : voxel(vec3(v.x, v.y + 1.0, v.z - 1.0))) > 0.0);
			}

			if ((a + b) == t) {
				if (i_args[0] == 0.0) {
					return i_color_index;
				} else {
					return i_args[0];
				}
			}
		}
	}

	return index;
}
