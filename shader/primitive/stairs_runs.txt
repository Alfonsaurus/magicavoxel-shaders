// MIT License (MIT)
// https://github.com/lachlanmcdonald/magicavoxel-shaders
// Copyright (c) 2021 Lachlan McDonald
//
// xs primitive/stairs_runs [Mode] [Direction] [Count] [Height] [X/Y Gap] [Z Gap]
//
// xs_begin
// author : '@lachlanmcdonald'
// arg : { id = '0'  name = 'Mode'  value = '0'  range = '0 3'  step = '1'  decimal = '0' }
// arg : { id = '1'  name = 'Direction'  value = '0'  range = '0 3'  step = '1'  decimal = '0' }
// arg : { id = '2'  name = 'Count'  value = '5'  range = '1 256'  step = '1'  decimal = '0' }
// arg : { id = '3'  name = 'Height'  value = '1'  range = '1 256'  step = '1'  decimal = '0' }
// arg : { id = '4'  name = 'X/Y Gap'  value = '0'  range = '0 256'  step = '1'  decimal = '0' }
// arg : { id = '5'  name = 'Z Gap'  value = '0'  range = '0 256'  step = '1'  decimal = '0' }
// xs_end

int mode = int(i_args[0]);
int direction = int(i_args[1]);
float count = min(i_args[2], i_volume_size.z);
float height = i_args[3];
float xy_gap = i_args[4];
float z_gap = i_args[5];

float map(vec3 v) {
	float volume_n;
	float vector_n;

	if (direction == 0) {
		vector_n = v.x;
		volume_n = i_volume_size.x;
	} else if (direction == 1) {
		vector_n = v.y;
		volume_n = i_volume_size.y;
	} else if (direction == 2) {
		vector_n = i_volume_size.x - v.x;
		volume_n = i_volume_size.x;
	} else if (direction == 3) {
		vector_n = i_volume_size.y - v.y;
		volume_n = i_volume_size.y;
	}

	if (mode == 1) {
		height = max(1.0, floor(i_volume_size.z / count));
	} else if (mode == 2) {
		count = max(2.0, floor(i_volume_size.z / height));
	} else if (mode == 3) {
		z_gap = max(0.0, floor(i_volume_size.z / (count + height)) - 1.0);
	}

	float w = volume_n / count;
	float d = floor(vector_n / w);
	float a = d * height + (d * z_gap);
	float b = d * height + height + (d * z_gap);

	if (vector_n - (d * w) <= (w - xy_gap)) {
		if (v.z > a && v.z < b) {
			return i_color_index;
		}
	}

	return 0.0;
}
